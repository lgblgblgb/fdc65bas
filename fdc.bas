10 IF (PEEK($D60F) AND 32) > 0 THEN MOUNT : REM MAKE SURE U8 IS UNMOUNTED ON REAL HARDWARE
11 BANK 128
20 TR=-1 : SC=-1

100 IF TR <  0 THEN TR =  0
101 IF TR > 90 THEN TR = 90
102 IF SC <  0 THEN SC =  0
103 IF SC > 90 THEN SC = 90
105 GOSUB 3000
110 OK=0
120 GETKEY A$
122 IF A$ = " " AND S = 0  AND OK = 1 THEN S = 256 : GOSUB 3000 : GOTO 2000
123 IF A$ = " " AND S <> 0 AND OK = 1 THEN S = 0   : GOSUB 3000 : GOTO 2000
130 IF A$ = "T" THEN GOSUB 3000 : INPUT "TRACK";TR  : S = 0 : GOTO 100
140 IF A$ = "S" THEN GOSUB 3000 : INPUT "SECTOR";SC : S = 0 : GOTO 100
150 IF A$ = "Q" THEN END : RUN
155 IF A$ = "M" THEN 4000
160 IF A$ <> "R" THEN 120

1000 REM *** READ SECTOR ***
1005 GOSUB 3000
1010 DR=$20
1020 REM IF SI<>0 THEN DR = DR + 8
1030 POKE $D080, DR
1040 POKE $D081, $20
1050 IF (PEEK($D082) AND $80) <> 0 THEN 1050
1060 POKE $D084, TR
1070 POKE $D085, SC+1
1080 POKE $D086, 0
1090 POKE $D081, $41
1100 IF (PEEK($D082) AND $80) <> 0 THEN 1100
1110 E=PEEK($D082) AND $18
1115 POKE $D080, $00
1120 IF E=0 THEN 2000 : REM NO ERROR, DO THE HEX DUMP STUFF

1900 REM ERROR
1910 POKE $D080, $40 : REM BLINK THE LED
1920 PRINT "ERROR=";HEX$(E)
1930 IF (E AND 8) THEN PRINT "CRC ERROR"
1940 IF (E AND $10) THEN PRINT "RNF ERROR"
1995 GOTO 120

2000 REM *** DO THE HEX DUMP ***
2001 POKE $D689, PEEK($D689) AND $7F
2010 FOR A=S TO S+255
2020 IF (A AND 15) = 0 THEN PRINT HEX$(A);"  "; : H$=""
2030 D = PEEK($FFD6C00+A)
2035 IF (D > 31 AND D < 128) OR (D > 159) THEN H$=H$+CHR$(D) : ELSE H$=H$+"."
2040 PRINT RIGHT$(HEX$(D),2);" ";
2050 IF (A AND 15) = 15 THEN PRINT " ";H$
2060 NEXT A
2200 PRINT : PRINT "DONE. SPACE = FLIP DUMP PAGE" : PRINT
2210 OK=1
2300 GOTO 120

3000 SCNCLR : PRINT "--- TRACK:";TR;" SECTOR:";SC;" --- MENU: T=TRACK, S=SECTOR, R=READ!, Q=QUIT" : PRINT : RETURN

4000 REM SPECIAL MONITOR MODE
4010 POKE $D080, $20 : REM TURN MOTOR ON
4020 IF (PEEK($D082) AND $80) <> 0 THEN 4020 : REM WAIT FOR SPIN
4030 PRINT PEEK($D082);" ";PEEK($D084);" ";PEEK($D085)
4040 GET A$ : IF A$="" THEN 4030
4050 POKE $D080, $00
4060 GOTO 100


