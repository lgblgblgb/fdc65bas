10 IF (PEEK($D60F) AND 32) > 0 THEN MOUNT : REM MAKE SURE U8 IS UNMOUNTED ON REAL HARDWARE
11 BANK 128
20 TR=-1 : SC=-1
30 GOTO 4000

100 IF TR <  0 THEN TR =  0
101 IF TR > 90 THEN TR = 90
102 IF SC <  0 THEN SC =  0
103 IF SC > 90 THEN SC = 90
105 GOSUB 3000
110 OK=0
120 GETKEY A$
122 IF A$ = " " AND S = 0  AND OK = 1 THEN S = 256 : GOSUB 3000 : GOTO 2000
123 IF A$ = " " AND S <> 0 AND OK = 1 THEN S = 0   : GOSUB 3000 : GOTO 2000
130 IF A$ = "T" THEN GOSUB 3000 : INPUT "TRACK";TR  : S = 0 : GOTO 100
140 IF A$ = "S" THEN GOSUB 3000 : INPUT "SECTOR";SC : S = 0 : GOTO 100
150 IF A$ = "Q" THEN END : RUN
155 IF A$ = "M" THEN 4000
160 IF A$ <> "R" THEN 120

1000 REM *** READ SECTOR ***
1005 GOSUB 3000
1010 DR=$20
1020 REM IF SI<>0 THEN DR = DR + 8
1030 POKE $D080, DR
1040 POKE $D081, $20
1050 IF (PEEK($D082) AND $80) <> 0 THEN 1050
1060 POKE $D084, TR
1070 POKE $D085, SC+1
1080 POKE $D086, 0
1090 POKE $D081, $41
1100 IF (PEEK($D082) AND $80) <> 0 THEN 1100
1110 E=PEEK($D082) AND $18
1115 POKE $D080, $00
1120 IF E=0 THEN 2000 : REM NO ERROR, DO THE HEX DUMP STUFF

1900 REM ERROR
1910 POKE $D080, $40 : REM BLINK THE LED
1920 PRINT "ERROR=";HEX$(E)
1930 IF (E AND 8) THEN PRINT "CRC ERROR"
1940 IF (E AND $10) THEN PRINT "RNF ERROR"
1995 GOTO 120

2000 REM *** DO THE HEX DUMP ***
2001 POKE $D689, PEEK($D689) AND $7F
2010 FOR A=S TO S+255
2020 IF (A AND 15) = 0 THEN PRINT HEX$(A);"  "; : H$=""
2030 D = PEEK($FFD6C00+A)
2035 IF (D > 31 AND D < 128) OR (D > 159) THEN H$=H$+CHR$(D) : ELSE H$=H$+"."
2040 PRINT RIGHT$(HEX$(D),2);" ";
2050 IF (A AND 15) = 15 THEN PRINT " ";H$
2060 NEXT A
2200 PRINT : PRINT "DONE. SPACE = FLIP DUMP PAGE" : PRINT
2210 OK=1
2300 GOTO 120

3000 SCNCLR : PRINT "--- TRACK:";TR;" SECTOR:";SC;" --- MENU: T=TRACK, S=SECTOR, R=READ!, Q=QUIT" : PRINT : RETURN




4000 REM SPECIAL MODE
4001 SCNCLR
4010 PRINT "MOTOR: ";PEEK($D080) AND $20;"(PRESS 0)"
4020 A=PEEK($D082)
4030 PRINT "TRACK0:";A AND 1
4040 PRINT "PROT:  ";A AND 2
4050 PRINT "LOST:  ";A AND 4
4060 PRINT "CRC:   ";A AND 8
4070 PRINT "RNF:   ";A AND 16
4080 PRINT "BUSY:  ";A AND 128
4100 A=PEEK($D083)
4110 PRINT "CHANGE:";A AND 1
4120 PRINT "INDEX: ";A AND 4
4130 PRINT "DISK:  ";A AND 8
4140 PRINT "AUTO-TRACK:";PEEK($D696) AND 128;"(PRESS 1)"
4150 A=PEEK($D6AE)
4160 PRINT "AUTO-DDHD: ";A AND 16;"(PRESS 2)"
4170 PRINT "AUTO-SPED: ";A AND 32;"(PRESS 3)"
4180 PRINT "SEL-HD:    ";A AND 64;"(PRESS 4)"
4190 PRINT "USE-TRBL:  ";A AND 128;"(PRESS 5)"


5000 PRINT : PRINT "--- PRESS X TO RETURN TO MAIN MENU ---"

5010 GET A$
5020 IF A$ = "X" THEN POKE $D080, $00 : GOTO 100
5030 IF A$ = "0" THEN POKE $D080, (PEEK($D080) XOR $20) AND $20 : GOTO 4000
5040 IF A$ = "1" THEN POKE $D696, (PEEK($D696) XOR $80) : GOTO 4000
5050 IF A$ = "2" THEN POKE $D6AE, (PEEK($D6AE) XOR 16) : GOTO 4000
5050 IF A$ = "3" THEN POKE $D6AE, (PEEK($D6AE) XOR 32) : GOTO 4000
5050 IF A$ = "4" THEN POKE $D6AE, (PEEK($D6AE) XOR 64) : GOTO 4000
5050 IF A$ = "5" THEN POKE $D6AE, (PEEK($D6AE) XOR 128) : GOTO 4000

6000 VSYNC 0 : REM LIMIT THE SCREEN UPDATE SPEED
6010 GOTO 4000

