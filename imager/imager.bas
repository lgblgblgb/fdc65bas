10 IF (PEEK($D60F) AND 32) > 0 THEN MOUNT : REM MAKE SURE U8 IS UNMOUNTED ON REAL HARDWARE
20 BANK 128
30 SCNCLR : PRINT "INSERT AN MS-DOS DISK AND PRESS SPACE"
40 GETKEY A$ : IF A$ <> " " THEN 40
50 PRINT
60 BU=$FFD6C00 : TG=$8000000
70 POKE $D689, PEEK($D689) AND $7F

100 PRINT "READING MBR"
110 TR=0:SC=1:HE=0:GOSUB 9000
115 POKE $D080,0
120 IF E<>0 THEN PRINT "SORRY." : END

130 MS=WPEEK(BU+$18) : MH=WPEEK(BU+$1A) : CP=80*MH*MS/2 : REM ASSUMING 80 TRACKS

140 PRINT "TRACKS=80 SECTORS=";MS;" HEADS=";MH;" CAPACITY=";CP;"K"
150 IF CP<>720 AND CP<>1440 THEN PRINT "BAD CAPACITY. NOT AN MS-DOS DISK?" : END
160 PRINT "PRESS SPACE TO START THE PROCESS"
170 GETKEY A$ : IF A$ <> " " THEN 170
180 EC=0

199 MT=80 : REM ASSUMING 80 TRACKS
200 FOR TR=0 TO MT-1
210 FOR HE=0 TO MH-1
220 FOR SC=1 TO MS
230 GOSUB 9000
240 EDMA 0,512,BU,TG
250 TG=TG+512
300 NEXT SC : NEXT HE : NEXT TR

1000 PRINT : PRINT "END. NUMBER OF ERRORS:";EC
1010 POKE $D080,0:END:RUN

9000 REM *** READ A SECTOR ***
9010 IF (PEEK($D080) AND $20) <> 0 THEN 9050 : REM MOTOR IS ALREADY ON
9020 PRINT "SPINNING UP"
9030 POKE $D080, $20 : POKE $D081, $20
9040 IF (PEEK($D082) AND $80) <> 0 THEN 9040
9050 PRINT "READING HEAD ";HE;" TRACK ";TR;" SECTOR ";SC;" ... ";
9100 POKE $D084,TR : POKE $D085,SC
9110 POKE $D086, HE
9115 IF HE = 0 THEN POKE $D080,$20 : ELSE POKE $D080,$28
9120 POKE $D081, $41
9130 E=PEEK($D082) : IF (E AND $80) <> 0 THEN 9130
9140 E=E AND $18
9150 IF E=0 THEN PRINT "OK" : RETURN
9160 PRINT "ERROR:";E
9165 EC=EC+1
9170 RETURN
